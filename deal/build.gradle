plugins {
    id 'org.springframework.boot' version '2.7.0'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'application'
    id 'org.liquibase.gradle' version '2.0.4'
    id 'jacoco'
    id 'org.springdoc.openapi-gradle-plugin' version '1.3.3'
    id 'com.github.johnrengelman.processes' version '0.5.0'
    id 'checkstyle'
}

group = 'code'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

application {
    mainClass = 'code.DealApplication'
}

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'org.projectlombok:lombok'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.liquibase:liquibase-core:4.11.0'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign:3.1.3'
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.7'
    implementation 'com.github.tomakehurst:wiremock:2.27.2'
    implementation 'org.springframework.boot:spring-boot-starter-validation:2.6.3'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'

    runtimeOnly 'org.postgresql:postgresql:42.3.6'

    liquibaseRuntime 'org.liquibase:liquibase-core:4.11.0'
    liquibaseRuntime 'ch.qos.logback:logback-core:1.2.11'
    liquibaseRuntime 'ch.qos.logback:logback-classic:1.2.11'
    liquibaseRuntime 'org.postgresql:postgresql'
    liquibaseRuntime 'org.liquibase.ext:liquibase-hibernate5:4.11.0'
    liquibaseRuntime 'org.springframework.data:spring-data-jpa'
    liquibaseRuntime 'org.springframework.boot:spring-boot'
    liquibaseRuntime 'org.yaml:snakeyaml:1.26'
    liquibaseRuntime sourceSets.main.output
}

diffChangeLog {
    dependsOn compileJava
}

liquibase {
    activities {
        main {
            changeLogFile 'src/main/resources/db/changelog/changelog-master.xml'
            url 'jdbc:postgresql://localhost:5432/loan-conveyor'
            username 'root'
            password 'root'
            referenceUrl 'hibernate:spring:code.model' +
                    '?dialect=org.hibernate.dialect.PostgreSQL10Dialect' +
                    '&hibernate.physical_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy'
            logLevel 'debug'
        }
        runList = "main"
    }
}

checkstyle {
    toolVersion '9.0'
}

test {
    useJUnitPlatform()
}

test {
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}